plugins {
    id 'java'
    id 'signing'
    id 'com.gradle.plugin-publish' version '1.3.1'
    id "io.freefair.lombok" version "8.13.1"
}

group 'run.halo.gradle'
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
}

publishing {
    repositories {
        maven {
            name = "internal"
            url = layout.buildDirectory.dir('repo/internal')
        }
    }
}

gradlePlugin {
    website = 'https://github.com/halo-sigs/halo-gradle-plugin'
    vcsUrl = 'https://github.com/halo-sigs/halo-gradle-plugin.git'

    plugins {
        create('haloPluginDevtoolsPlugin') {
            id = 'run.halo.plugin.devtools'
            displayName = 'Halo Plugin Devtools'
            description = 'A plugin devtools for Halo plugin'
            implementationClass = 'run.halo.gradle.HaloDevtoolsPlugin'
            tags.addAll('halo', 'java', 'devtools')
        }
    }
}

signing {
    sign publishing.publications
}

ext {
    jackjson = '2.14.0'
    lang3 = '3.12.0'
    semver4j = '0.9.0'
    asm = '9.8'
    dockerJava = '3.3.0'
    jsonPatch = '1.13'
    openApiGenerator = '7.7.0'
}

dependencies {
    implementation("com.github.docker-java:docker-java-transport-httpclient5:$dockerJava") {
        exclude group: 'org.slf4j'
    }
    implementation("com.github.docker-java:docker-java-core:$dockerJava") {
        exclude group: 'org.slf4j'
        exclude group: 'com.fasterxml.jackson.core'
    }

    implementation("org.openapitools:openapi-generator:$openApiGenerator")

    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jackjson"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jackjson"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackjson"
    implementation "org.apache.commons:commons-lang3:$lang3"
    implementation "com.github.zafarkhaja:java-semver:$semver4j"
    implementation "org.ow2.asm:asm:$asm"
    implementation "org.ow2.asm:asm-commons:$asm"
    implementation 'commons-io:commons-io:2.11.0'
    implementation "com.github.java-json-tools:json-patch:$jsonPatch"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'org.skyscreamer:jsonassert:1.5.1'
}

test {
    useJUnitPlatform()
}
